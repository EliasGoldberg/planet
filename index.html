<!DOCTYPE html>
<html lang="en">
<head>
  <meta charset="UTF-8">
  <title>threeDee</title>
</head>
<body>
<canvas id='gl'
        style='width:400px; height:300px;
                   border: solid 1px black;' />
<script src="http://code.jquery.com/jquery-2.1.1.min.js"></script>
<script src="http://coffeescript.org/extras/coffee-script.js"></script>
<script type="text/coffeescript">
  main = () ->
    canvas = document.getElementById('gl')
    gl = canvas.getContext('experimental-webgl')
    program = new Program(gl)

    program.addShader(gl.VERTEX_SHADER,'''
      attribute vec4 a_Position;
      attribute float a_PointSize;
      void main() {
           gl_Position = a_Position;
           gl_PointSize = a_PointSize;
      }
    ''')

    program.addShader(gl.FRAGMENT_SHADER,'''
      void main() {
        gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0);
      }
    ''')

    program.activate()

    gl.clearColor(0.0,0.0,0.0,1.0)
    gl.clear(gl.COLOR_BUFFER_BIT)
    n = program.setAttribPointer('a_Position',[0.0, 0.5, -0.5, -0.5, 0.5, -0.5])
    program.setAttrib('a_PointSize',10)
    gl.drawArrays(gl.POINTS, 0, n)

  class Program
    constructor: (@gl) -> @program = @gl.createProgram()

    addShader: (type,source) ->
      shader = @gl.createShader(type)
      @gl.shaderSource(shader,source)
      @gl.compileShader(shader)
      message = @gl.getShaderInfoLog(shader)
      console.log message if message? and message != ''
      @gl.attachShader(@program,shader)

    activate: ->
      @gl.linkProgram(@program)
      message = @gl.getProgramInfoLog(@program)
      console.log message if message? and message != ''
      @gl.useProgram(@program)

    setAttrib: (name,value) ->
      attrib = @gl.getAttribLocation(@program,name)
      # must get correct version of the vertexAttrib method based on value
      isVector = value.length?
      size = if isVector then value.length else 1
      vee = if isVector then 'v' else ''
      @gl["vertexAttrib#{size}f#{vee}"](attrib,value)

    setAttribPointer:(name,values) ->
      attrib = @gl.getAttribLocation(@program,name)
      vertexBuffer = @gl.createBuffer();
      @gl.bindBuffer(@gl.ARRAY_BUFFER, vertexBuffer);
      @gl.bufferData(@gl.ARRAY_BUFFER, new Float32Array(values), @gl.STATIC_DRAW)
      @gl.vertexAttribPointer(attrib, 2, @gl.FLOAT, false, 0, 0)
      @gl.enableVertexAttribArray(attrib)
      values.length / 2

  $(main)
</script>
</body>
</html>